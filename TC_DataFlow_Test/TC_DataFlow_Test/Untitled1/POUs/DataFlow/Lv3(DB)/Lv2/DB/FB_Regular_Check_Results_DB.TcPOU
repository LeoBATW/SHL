<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_Regular_Check_Results_DB" Id="{8f70e832-a4e2-46f2-8870-c6a4c1398458}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_Regular_Check_Results_DB
VAR_INPUT
	udiDBID : UDINT := 1 ;
END_VAR

VAR_OUTPUT
	bBusy 		: BOOL;
	bDone		: BOOL;
	bError		: BOOL;
	udiErrId	: UDINT;
END_VAR

VAR
	// FBs
	FB_PLCDBCmdEvt_regular_check_results : FB_PLCDBCmdEvt(sNetID := '' , tTimeout := T#10S) ;
	
	// Vars
	stPara_regular_check_results 	: ARRAY[0..11] OF ST_ExpParameter ;
	iStep							: INT;
	iErrStep						: INT := 9000;
	stRcResultsDB 					: ST_RegularCheck_Results_DB ;
	
	// SQL Command
	sCmd_insert_regular_check_results 	: STRING(2000) ; //由M_CreateSQLCmd自動產生
	
	
	
	
	// Time
	tStartTime	: TIME;
	tEndTime	: TIME;
	tDuration	: TIME;	
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[]]></ST>
    </Implementation>
    <Method Name="M_ClearStatus" Id="{fe3aa92b-4758-46bf-a6f5-10ba6ddc54cf}">
      <Declaration><![CDATA[METHOD PRIVATE M_ClearStatus : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[bBusy := FALSE;
bDone := FALSE;
bError := FALSE;
udiErrId := 0;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_CreateSQLCmd" Id="{bff0d867-582b-490f-8259-d19c9ab8bc54}">
      <Declaration><![CDATA[METHOD PRIVATE M_CreateSQLCmd : BOOL
VAR_INPUT
	stRcResults : ST_RegularCheck_Results;
END_VAR
VAR_OUTPUT
	sSqlCmdOut	: STRING(2000);
END_VAR

VAR
	fbFormatBody   	: FB_FormatString2;
	
	sFormatBody : STRING(1000) := '
		INSERT INTO [dbo].[regular_check_results] 
					([station_number], [track_number], [start_time], [sensor], [camera], [loadcell], [sensor2], [regular_check_id], [faam_id], [so_number], [so_id], [employee_name_logged_in]) 
		VALUES 		({di_station_number}, {di_track_number}, {dt_start_time}, %s, %s, %s, %s, {di_regular_check_id}, $'{str_faam_id}$', $'{str_so_number}$', {di_so_id}, $'{wstr_employee_name_logged_in}$') ' ;
	
	sSensor		: T_MaxString := '';
	sCamera		: T_MaxString := '';
	sLoadcell	: T_MaxString := '';
	sSensor2	: T_MaxString := '';
	
	sOut				: STRING(2000);
	sOutEmpty			: STRING(2000);
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[// Sensor
IF stRcResults.eSensor = E_RegularCheckSensorResult.Null THEN
	sSensor := 'NULL';
ELSE
	sSensor := '{b_sensor}';
END_IF

// Camera
IF stRcResults.eCamera = E_RegularCheckSensorResult.Null THEN
	sCamera := 'NULL';
ELSE
	sCamera := '{b_camera}';
END_IF

// Loadcell
IF stRcResults.eLoadcell = E_RegularCheckSensorResult.Null THEN
	sLoadcell := 'NULL';
ELSE
	sLoadcell := '{b_loadcell}';
END_IF

// Sensor2
IF stRcResults.eSensor2 = E_RegularCheckSensorResult.Null THEN
	sSensor2 := 'NULL';
ELSE
	sSensor2 := '{b_sensor2}';
END_IF


sOut := sOutEmpty;
fbFormatBody(
		pFormatString:= ADR(sFormatBody), 
		arg1:= F_STRING( sSensor ), 
		arg2:= F_STRING( sCamera ), 
		arg3:= F_STRING( sLoadcell ), 
		arg4:= F_STRING( sSensor2 ), 
		arg5:= , 
		arg6:= , 
		arg7:= , 
		arg8:= , 
		arg9:= , 
		arg10:= , 
		pDstString:= ADR(sOut), 
		nDstSize:= SIZEOF(sOut), 
		bError=> , 
		nErrId=> );
		
		
IF (NOT fbFormatBody.bError) THEN
	sSqlCmdOut := sOut;
	M_CreateSQLCmd := TRUE;
ELSE
	M_CreateSQLCmd := FALSE;
END_IF]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_FillUploadData" Id="{59afbabb-bfd5-420e-847d-f8f329e7adff}">
      <Declaration><![CDATA[METHOD PRIVATE M_FillUploadData : BOOL
VAR_INPUT
	stRcResults : ST_RegularCheck_Results;
END_VAR
VAR_OUTPUT
	stRcResultsDB 	: ST_RegularCheck_Results_DB ;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stRcResultsDB.diStation_number          	:= stRcResults.diStation_number;
stRcResultsDB.diTrack_number            	:= stRcResults.diTrack_number;
stRcResultsDB.dtStart_time              	:= stRcResults.dtStart_time;
stRcResultsDB.diRegular_check_id        	:= stRcResults.diRegular_check_id;
stRcResultsDB.sFaam_id                 		:= stRcResults.sFaam_id;
stRcResultsDB.sSo_number               		:= stRcResults.sSo_number;
stRcResultsDB.diSo_id                   	:= stRcResults.diSo_id;
stRcResultsDB.wsEmployee_name_logged_in 	:= stRcResults.wsEmployee_name_logged_in;

// bSensor
IF stRcResults.eSensor = E_RegularCheckSensorResult.Pass THEN
	stRcResultsDB.bSensor := TRUE;
ELSE
	stRcResultsDB.bSensor := FALSE;
END_IF


// bCamera
IF stRcResults.eCamera = E_RegularCheckSensorResult.Pass THEN
	stRcResultsDB.bCamera := TRUE;
ELSE
	stRcResultsDB.bCamera := FALSE;
END_IF


// bLoadcell
IF stRcResults.eLoadcell = E_RegularCheckSensorResult.Pass THEN
	stRcResultsDB.bLoadcell := TRUE;
ELSE
	stRcResultsDB.bLoadcell := FALSE;
END_IF


// bSensor2
IF stRcResults.eSensor2 = E_RegularCheckSensorResult.Pass THEN
	stRcResultsDB.bSensor2 := TRUE;
ELSE
	stRcResultsDB.bSensor2 := FALSE;
END_IF


M_FillUploadData := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Reset" Id="{457f3f93-460d-46f0-9723-8dd8bce5048e}">
      <Declaration><![CDATA[METHOD M_Reset : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[iStep := 0;
M_ClearStatus();
]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_SetBusy" Id="{26bc23b2-e23b-4a53-bee9-3c9fceda9a36}">
      <Declaration><![CDATA[METHOD PRIVATE M_SetBusy : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[bError := FALSE;
bBusy := TRUE;
bDone := FALSE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_SetDone" Id="{0ae61bb4-9f00-438e-91c4-cde8078ff504}">
      <Declaration><![CDATA[METHOD PRIVATE M_SetDone : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[bError := FALSE;
bBusy := FALSE;
bDone := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_SetError" Id="{1c555e13-f59c-4d8a-988a-5f53c463fb32}">
      <Declaration><![CDATA[METHOD PRIVATE M_SetError : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[bError := TRUE;
bBusy := FALSE;
bDone := FALSE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_SetSQLCmdPara" Id="{76eebbc3-0d15-4e00-aa95-9832b55bc5fe}">
      <Declaration><![CDATA[METHOD Private M_SetSQLCmdPara : BOOL
VAR_INPUT
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[stPara_regular_check_results[0].sParaName := 'di_station_number' ;
stPara_regular_check_results[0].eParaType := E_ExpParameterType.Int32 ;
stPara_regular_check_results[0].nParaSize := SIZEOF(ST_RegularCheck_Results_DB.diStation_number);
stPara_regular_check_results[1].sParaName := 'di_track_number' ;
stPara_regular_check_results[1].eParaType := E_ExpParameterType.Int32 ;
stPara_regular_check_results[1].nParaSize := SIZEOF(ST_RegularCheck_Results_DB.diTrack_number);
stPara_regular_check_results[2].sParaName := 'dt_start_time' ;
stPara_regular_check_results[2].eParaType := E_ExpParameterType.DateTime ;
stPara_regular_check_results[2].nParaSize := SIZEOF(ST_RegularCheck_Results_DB.dtStart_time);
stPara_regular_check_results[3].sParaName := 'b_sensor' ;
stPara_regular_check_results[3].eParaType := E_ExpParameterType.Boolean ;
stPara_regular_check_results[3].nParaSize := SIZEOF(ST_RegularCheck_Results_DB.bSensor);
stPara_regular_check_results[4].sParaName := 'b_camera' ;
stPara_regular_check_results[4].eParaType := E_ExpParameterType.Boolean ;
stPara_regular_check_results[4].nParaSize := SIZEOF(ST_RegularCheck_Results_DB.bCamera);
stPara_regular_check_results[5].sParaName := 'b_loadcell' ;
stPara_regular_check_results[5].eParaType := E_ExpParameterType.Boolean ;
stPara_regular_check_results[5].nParaSize := SIZEOF(ST_RegularCheck_Results_DB.bLoadcell);
stPara_regular_check_results[6].sParaName := 'b_sensor2' ;
stPara_regular_check_results[6].eParaType := E_ExpParameterType.Boolean ;
stPara_regular_check_results[6].nParaSize := SIZEOF(ST_RegularCheck_Results_DB.bSensor2);
stPara_regular_check_results[7].sParaName := 'di_regular_check_id' ;
stPara_regular_check_results[7].eParaType := E_ExpParameterType.Int32 ;
stPara_regular_check_results[7].nParaSize := SIZEOF(ST_RegularCheck_Results_DB.diRegular_check_id);
stPara_regular_check_results[8].sParaName := 'str_faam_id' ;
stPara_regular_check_results[8].eParaType := E_ExpParameterType.STRING_ ;
stPara_regular_check_results[8].nParaSize := SIZEOF(ST_RegularCheck_Results_DB.sFaam_id);
stPara_regular_check_results[9].sParaName := 'str_so_number' ;
stPara_regular_check_results[9].eParaType := E_ExpParameterType.STRING_ ;
stPara_regular_check_results[9].nParaSize := SIZEOF(ST_RegularCheck_Results_DB.sSo_number);
stPara_regular_check_results[10].sParaName := 'di_so_id' ;
stPara_regular_check_results[10].eParaType := E_ExpParameterType.Int32 ;
stPara_regular_check_results[10].nParaSize := SIZEOF(ST_RegularCheck_Results_DB.diSo_id);
stPara_regular_check_results[11].sParaName := 'wstr_employee_name_logged_in' ;
stPara_regular_check_results[11].eParaType := E_ExpParameterType.WSTRING_ ;
stPara_regular_check_results[11].nParaSize := SIZEOF(ST_RegularCheck_Results_DB.wsEmployee_name_logged_in);

M_SetSQLCmdPara := TRUE;]]></ST>
      </Implementation>
    </Method>
    <Method Name="M_Upload" Id="{39991fdc-8623-4d1f-84e2-86bfddb53830}">
      <Declaration><![CDATA[METHOD M_Upload : BOOL
VAR_INPUT
	stRcResults : ST_RegularCheck_Results ;
END_VAR
VAR_INST
	ITF_TcMessage : Tc3_EventLogger.I_TcMessage;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[CASE iStep OF
	0: 
		// SQL Cmd的變數設定
		M_SetSQLCmdPara();
		M_FillUploadData(stRcResults := stRcResults, stRcResultsDB => stRcResultsDB);
		
		tStartTime := TIME();
		M_SetBusy();
		
		// SQL Command 產生
		IF M_CreateSQLCmd(stRcResults := stRcResults, sSqlCmdOut => sCmd_insert_regular_check_results) THEN
			iStep := iStep + 100;
		END_IF
		
	100:
		IF FB_PLCDBCmdEvt_regular_check_results.Execute
			(
				hDBID        := udiDBID ,
			 	pExpression  := ADR(sCmd_insert_regular_check_results) , //SQL 指令的字串
			 	cbExpression := SIZEOF(sCmd_insert_regular_check_results) ,
			 	pData        := ADR(stRcResultsDB) , //SQL 指令字串中的變數值，比如Keyword1數值是0，會帶入sCmd中的{Keyword}
			 	cbData       := SIZEOF(stRcResultsDB) ,
			 	pParameter   := ADR(stPara_regular_check_results) , //SQL 指令字串中的變數的類型，比如sParaName為'Keyword'
			 	cbParameter  := SIZEOF(stPara_regular_check_results) ,
			)
		THEN
			IF FB_PLCDBCmdEvt_regular_check_results.bError THEN // 異常
				ITF_TcMessage := FB_PLCDBCmdEvt_regular_check_results.ipTcResult;
				udiErrid := ITF_TcMessage.nEventId;	
				M_SetError();		
				iStep := iErrStep + 100;
			ELSE // 成功寫入
				M_Upload := TRUE;
				tEndTime := TIME();
				tDuration := tEndTime - tStartTime;
				M_SetDone();
				iStep := 0;
			END_IF
		END_IF

END_CASE

]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_Regular_Check_Results_DB">
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_Regular_Check_Results_DB.M_ClearStatus">
      <LineId Id="6" Count="0" />
      <LineId Id="5" Count="0" />
      <LineId Id="7" Count="1" />
    </LineIds>
    <LineIds Name="FB_Regular_Check_Results_DB.M_CreateSQLCmd">
      <LineId Id="504" Count="0" />
      <LineId Id="476" Count="0" />
      <LineId Id="483" Count="0" />
      <LineId Id="485" Count="1" />
      <LineId Id="484" Count="0" />
      <LineId Id="496" Count="0" />
      <LineId Id="477" Count="0" />
      <LineId Id="487" Count="3" />
      <LineId Id="478" Count="0" />
      <LineId Id="495" Count="0" />
      <LineId Id="479" Count="0" />
      <LineId Id="491" Count="3" />
      <LineId Id="480" Count="0" />
      <LineId Id="503" Count="0" />
      <LineId Id="481" Count="0" />
      <LineId Id="499" Count="3" />
      <LineId Id="497" Count="1" />
      <LineId Id="482" Count="0" />
      <LineId Id="475" Count="0" />
      <LineId Id="453" Count="14" />
      <LineId Id="5" Count="0" />
      <LineId Id="505" Count="3" />
      <LineId Id="510" Count="2" />
      <LineId Id="509" Count="0" />
    </LineIds>
    <LineIds Name="FB_Regular_Check_Results_DB.M_FillUploadData">
      <LineId Id="70" Count="2" />
      <LineId Id="77" Count="3" />
      <LineId Id="53" Count="0" />
      <LineId Id="98" Count="0" />
      <LineId Id="81" Count="1" />
      <LineId Id="87" Count="0" />
      <LineId Id="89" Count="1" />
      <LineId Id="88" Count="0" />
      <LineId Id="112" Count="0" />
      <LineId Id="99" Count="0" />
      <LineId Id="91" Count="0" />
      <LineId Id="94" Count="3" />
      <LineId Id="92" Count="0" />
      <LineId Id="111" Count="0" />
      <LineId Id="93" Count="0" />
      <LineId Id="100" Count="4" />
      <LineId Id="84" Count="0" />
      <LineId Id="110" Count="0" />
      <LineId Id="85" Count="0" />
      <LineId Id="105" Count="4" />
      <LineId Id="86" Count="0" />
      <LineId Id="83" Count="0" />
      <LineId Id="62" Count="0" />
      <LineId Id="61" Count="0" />
    </LineIds>
    <LineIds Name="FB_Regular_Check_Results_DB.M_Reset">
      <LineId Id="14" Count="0" />
      <LineId Id="10" Count="0" />
      <LineId Id="8" Count="0" />
    </LineIds>
    <LineIds Name="FB_Regular_Check_Results_DB.M_SetBusy">
      <LineId Id="6" Count="1" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_Regular_Check_Results_DB.M_SetDone">
      <LineId Id="6" Count="1" />
      <LineId Id="5" Count="0" />
    </LineIds>
    <LineIds Name="FB_Regular_Check_Results_DB.M_SetError">
      <LineId Id="6" Count="2" />
    </LineIds>
    <LineIds Name="FB_Regular_Check_Results_DB.M_SetSQLCmdPara">
      <LineId Id="255" Count="34" />
      <LineId Id="218" Count="0" />
      <LineId Id="215" Count="0" />
      <LineId Id="202" Count="0" />
    </LineIds>
    <LineIds Name="FB_Regular_Check_Results_DB.M_Upload">
      <LineId Id="682" Count="4" />
      <LineId Id="737" Count="0" />
      <LineId Id="728" Count="0" />
      <LineId Id="687" Count="0" />
      <LineId Id="739" Count="0" />
      <LineId Id="743" Count="0" />
      <LineId Id="740" Count="2" />
      <LineId Id="689" Count="18" />
      <LineId Id="726" Count="0" />
      <LineId Id="729" Count="1" />
      <LineId Id="727" Count="0" />
      <LineId Id="708" Count="0" />
      <LineId Id="710" Count="1" />
      <LineId Id="717" Count="2" />
      <LineId Id="620" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>